model RBAC

class OP
end

class OBJ
end

class P
attributes
  op : OP
  obj : OBJ
end

class R
attributes
  name : String
operations
  getRoles() : Set(R) = Set{self}
end

class HR < R
operations
  getRoles() : Set(R) = Set{self}->union(juniors()) // overwrite
  juniors() : Set(HR) = junior->closure(junior)->asSet
  seniors() : Set(HR) = senior->closure(senior)->asSet
constraints
  inv nonCyclicRoleHierarchy: juniors()->excludes(self)
end

association RH between
  HR[*] role junior
  HR[*] role senior
end

class S
attributes
  user : U
constraints
inv onlyActiveAssignedRoles:
  user.r->includesAll(activeRoles)
end

class U
attributes
  name : String
end

association UA between
  U[*]
  R[*]
end

association PA between
  P[*]
  R[*]
end

aggregation SR between
  S[0..1]
  R[*] role activeRoles
end

class CP
end

aggregation CPP between
  CP[0..1]
  P[*]
end

class CR
end

aggregation CRR between
  CR[0..1]
  R[*]
end

class CU
end

aggregation CUU between
  CU[0..1]
  U[*]
end

class RBAC

operations
  OBJ() : Set(OBJ) = OBJ.allInstances
  OP() : Set(OP) = OP.allInstances
  P() : Set(P) = P.allInstances
  R() : Set(R) = R.allInstances
  S() : Set(S) = S.allInstances
  U() : Set(U) = U.allInstances
  CP() : Set(Set(P)) = CP.allInstances->collectNested(p)->asSet
  CR() : Set(Set(R)) = CR.allInstances->collectNested(r)->asSet
  CU() : Set(Set(U)) = CU.allInstances->collectNested(u)->asSet

  uroles(us : Set(U)): Set(R) = us->collect(r)->asSet
  proles(ps : Set(P)): Set(R) = ps->collect(r)->asSet
  sroles(ss : Set(S)): Set(R) = ss->collect(activeRoles)->asSet
  uroles_(us : Set(U)): Set(R) = uroles(us)->collect(getRoles())->asSet
  proles_(ps : Set(P)): Set(R) = proles(ps)->collect(getRoles())->asSet
  sroles_(ss : Set(S)): Set(R) = sroles(ss)->collect(getRoles())->asSet
  user(s : S): U = s.user
  users(rs : Set(R)): Set(U) = rs->collect(u)->asSet
  sessions(us : Set(U)): Set(S) = S()->select(s | us->includes(s.user))
  permissions(rs : Set(R)): Set(P) = rs->collect(p)->asSet
  permissions_(rs : Set(R)): Set(P) =
    permissions(rs->collect(getRoles())->asSet)
  ops(rs : Set(R), os : Set(OBJ)): Set(OP) =
    permissions(rs)->select(p | os->includes(p.obj))->collect(op)->asSet
  objects(ps : Set(P)): Set(OBJ) = ps->collect(obj)->asSet
end

constraints
context RBAC
