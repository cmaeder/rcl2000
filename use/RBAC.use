model RBAC

class OP
end

class OBJ
end

class P
attributes
  op : OP
  obj : OBJ
end

class R
attributes
  name : String
operations
  getRoles() : Set(R) = Set{self}
end

class HR < R
operations
  getRoles() : Set(R) = Set{self}->union(juniors()) // overwrite
  juniors() : Set(HR) = junior->closure(junior)->asSet
  seniors() : Set(HR) = senior->closure(senior)->asSet
constraints
  inv nonCyclicRoleHierarchy: juniors()->excludes(self)
end

association RH between
  HR[*] role junior
  HR[*] role senior
end

class S
attributes
  user : U
  activeRoles : Set(R)
constraints
inv onlyActiveAssignedRoles:
  user.r->includesAll(activeRoles)
end

class U
attributes
  name : String
end

association UA between
  U[*]
  R[*]
end

association PA between
  P[*]
  R[*]
end

class CP
attributes
 cs : Set(P)
end

class CR
attributes
 cs : Set(R)
end

class CU
attributes
 cs : Set(U)
end

class RBAC

attributes
  OBJ : Set(OBJ) init = OBJ.allInstances
  OP : Set(OP) init = OP.allInstances
  P : Set(P) init = P.allInstances
  R : Set(R) init = R.allInstances
  S : Set(S) init = S.allInstances
  U : Set(U) init = U.allInstances
  CP : Set(Set(P)) init = CP.allInstances->collectNested(cs)->asSet
  CR : Set(Set(R)) init = CR.allInstances->collectNested(cs)->asSet
  CU : Set(Set(U)) init = CU.allInstances->collectNested(cs)->asSet

operations
  uroles(us : Set(U)): Set(R) = us->collect(r)->asSet
  proles(ps : Set(P)): Set(R) = ps->collect(r)->asSet
  sroles(ss : Set(S)): Set(R) = ss->collect(activeRoles)->asSet
  uroles_(us : Set(U)): Set(R) = uroles(us)->collect(getRoles())->asSet
  proles_(ps : Set(P)): Set(R) = proles(ps)->collect(getRoles())->asSet
  sroles_(ss : Set(S)): Set(R) = sroles(ss)->collect(getRoles())->asSet
  user(s : S): U = s.user
  users(rs : Set(R)): Set(U) = rs->collect(u)->asSet
  sessions(us : Set(U)): Set(S) = S->select(s | us->includes(s.user))
  permissions(rs : Set(R)): Set(P) = rs->collect(p)->asSet
  permissions_(rs : Set(R)): Set(P) =
    permissions(rs->collect(getRoles())->asSet)
  ops(rs : Set(R), os : Set(OBJ)): Set(OP) =
    permissions(rs)->select(p | os->includes(p.obj))->collect(op)->asSet
  objects(ps : Set(P)): Set(OBJ) = ps->collect(obj)->asSet
end

constraints
context RBAC
